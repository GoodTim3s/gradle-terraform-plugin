plugins {
    id 'idea'
    id 'org.ysb33r.os' version '1.0.0'
    id 'org.asciidoctor.jvm.convert' version '3.2.0' apply false
    id 'org.asciidoctor.editorconfig' version '3.2.0' apply false
    id 'com.gradle.plugin-publish' version '0.15.0' apply false
    id 'com.github.hierynomus.license' version '0.16.1' apply false
    id 'org.ajoberstar.git-publish' version '3.0.1' apply false
}

allprojects {
    ext {
        spockGroovyVer = GroovySystem.version.replaceAll(/\.\d+$/, '')
        notSnapshot = { !version.endsWith('-SNAPSHOT') }
        terraformTestCacheDir = file("${rootProject.buildDir}/terraform-binaries")

        codedTerraformVersion = (project(':terraform-gradle-base')
            .file('src/main/groovy/org/ysb33r/gradle/terraform/TerraformExtension.groovy')
            .readLines().grep {
            it =~ /static\s+final\s+String\s+TERRAFORM_DEFAULT\s+=/
        })[0].replaceFirst(~/^(.+?["'])/, '').replaceFirst(~/(["'].*)$/, '')

        // Always list the primary test version as the first one in this list.
        terraformTestVersions = [codedTerraformVersion, '0.12.24']
    }

    tasks.withType(Test).configureEach {
        systemProperties TERRAFORM_VERSION: terraformTestVersions[0]
    }

    repositories {
        mavenCentral() {
            content {
                excludeGroup 'terraform'
                excludeGroup 'terraform-provider'
            }
        }

        if (!notSnapshot()) {
            mavenLocal()
        }
    }
}
