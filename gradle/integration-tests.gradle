
configurations {
    integrationTestImplementation {
        extendsFrom testImplementation
    }
    integrationTestRuntimeOnly {
        extendsFrom integrationTestImplementation, testRuntimeOnly
    }
}

sourceSets {
    integrationTest {
        java.srcDir file("src/integrationTest/java")
        groovy.srcDir file("src/integrationTest/groovy")
        resources.srcDir file("src/integrationTest/resources")
        compileClasspath = sourceSets.main.output + configurations.integrationTestCompileClasspath
        runtimeClasspath = output + compileClasspath + configurations.integrationTestRuntimeClasspath
    }
}

task integrationTest(type: Test, dependsOn: jar) {
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
    reports.html.destination = file("${reporting.baseDir}/integrationTest")

    systemProperties TERRAFORM_CACHE_DIR: terraformTestCacheDir.absolutePath
    systemProperties TERRAFORM_VERSION: terraformTestVersions[0]
    systemProperties RESOURCES_DIR: file('src/integrationTest/resources').absolutePath
    systemProperties 'org.ysb33r.gradle.hashicorp.releases.uri': terraformTestCacheDir.absoluteFile.toURI()

    mustRunAfter test

    enabled = !project.gradle.startParameter.isOffline()
}


check.dependsOn integrationTest

