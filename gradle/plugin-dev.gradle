apply plugin: 'maven-publish'

dependencies {
    implementation "org.codehaus.groovy:groovy-all:${groovyVer}"
    implementation gradleApi()
    api "org.ysb33r.gradle:grolifant-herd:${grolifantVer}"

    testImplementation("org.spockframework:spock-core:${spockMainVer}-groovy-${spockGroovyVer}") {
        exclude module: 'groovy-all'
    }

    testImplementation "org.codehaus.groovy:groovy-all:${groovyVer}"
    testImplementation gradleTestKit()
    testImplementation "org.ysb33r.gradle:grolifant-herd:${grolifantVer}"
}

gradleTest {
    versions gradleTestMinVersion

    // tag::gradleTestVersions[]
    versions '6.5.1', '7.5.1', '8.1.1'
    // end::gradleTestVersions[]

    systemProperties 'org.ysb33r.gradle.hashicorp.releases.uri': terraformTestCacheDir.absoluteFile.toURI()
    dependsOn jar, ':cacheTerraformBinaries'
    shouldRunAfter 'integrationTest'
    testKitStrategy = directoryPerTest

//    gradleArguments '-d'
}

license {
    header = rootProject.file('gradle/license/HEADER')
    strictCheck = true
    ignoreFailures = false
    mapping {
        java = 'SLASHSTAR_STYLE'
        groovy = 'SLASHSTAR_STYLE'
    }
    ext.year = '2017-2022'
    excludes([
        '**/*.adoc', '**/*.md',
        '**/*.properties',
        '**/*dsl.*Spec.groovy',
        '**/*.json',
        '**/*.tf'
    ])
}

pluginBundle {
    mavenCoordinates {
        groupId = project.group
    }
}

publishPlugins {
    onlyIf notSnapshot
    mustRunAfter check, ':docs:installDocs'
}

apply from: rootProject.file('gradle/integration-tests.gradle')